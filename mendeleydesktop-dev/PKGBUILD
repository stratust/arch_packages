# Maintainer: Thiago Yukio Kikuchi Oliveira <stratust@gmail.com>

pkgname=mendeleydesktop-dev
_pkgname=mendeleydesktop
pkgver=1.5.5
_pkgver=1.5-dev5
pkgrel=1
pkgdesc="Academic software for managing and sharing research papers (desktop client)"
url="http://www.mendeley.com/download-mendeley-desktop/preview/"
arch=(i686 x86_64)

license=(custom:mendeley_eula)
depends=(libpng python2 qt)
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
install=mendeleydesktop.install
_arch=$CARCH
[[ $CARCH = i686 ]] && _arch=i486

source=("http://download.mendeley.com/linux/$_pkgname-$_pkgver-linux-$_arch.tar.bz2")
md5sums=('f66cfffd1ce6cc50aae814c3d99e231a')

if [[ $CARCH = x86_64 ]]; then
	md5sums=('468da7f4c8d9d3b76e5e8399f5327232')
fi

package() {
	cd "$_pkgname-$_pkgver-linux-$_arch"

	find share/doc/mendeleydesktop/ -iname "*.txt" -delete
	install -dm755 "$pkgdir/opt/$pkgname/bin"
	rm -rf lib/qt
	mv lib share "$pkgdir/opt/$pkgname/"
	#ln -s "/usr/lib" "$pkgdir/opt/$pkgname/lib/qt"

	sed -i 's@^\s*subprocess\.Popen(\[MENDELEY_BASE_PATH+"/bin/install-mendeley-link-handler\.sh".*$@#&@' "bin/$_pkgname"
	install -Dm755 "bin/$_pkgname" "$pkgdir/opt/$pkgname/bin/$_pkgname"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -dm755 "$pkgdir/usr/bin/"
	ln -s "/opt/$pkgname/bin/$_pkgname" "$pkgdir/usr/bin/$_pkgname"

	install -dm755 "$pkgdir/usr/share/applications"
	ln -s "/opt/$pkgname/share/applications/mendeleydesktop.desktop" "$pkgdir/usr/share/applications/"

	for size in 16 22 32 48 64 128; do
	    install -dm755 "$pkgdir/usr/share/icons/hicolor/${size}x${size}/apps"
	    ln -s "/opt/$pkgname/share/icons/hicolor/${size}x${size}/apps/$_pkgname.png" \
		  "$pkgdir/usr/share/icons/hicolor/${size}x${size}/apps"
	done
}
